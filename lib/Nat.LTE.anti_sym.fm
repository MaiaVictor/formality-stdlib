Nat.LTE.anti_sym
: {a : (Nat a)} {b : (Nat b)} {p1 : (Nat.LTE a b p1)} {p2 : (Nat.LTE b a p2)} (Eq Nat a b (Nat.LTE.anti_sym a b p1 p2))
= [a] [b] [p1] [p2]
  let x1 (Nat.LTE.fst a b p1)
  let x2 (Nat.LTE.fst b a p2)
  let e1 (Nat.LTE.snd a b p1)
  let e2 (Nat.LTE.snd b a p2)
  let e3
  (Nat.add_no_inverse_l x1 x2
    (Nat.add_unique_neutral a (Nat.add x1 x2)
      (Eq.trans Nat (Nat.add a (Nat.add x1 x2)) (Nat.add (Nat.add a x1) x2) a
        (Nat.add_assoc_r a x1 x2)
        (Eq.trans Nat
          (Nat.add (Nat.add a x1) x2) (Nat.add b x2) a (Eq.cong Nat Nat (Nat.add a x1) b e1 [x : (Nat x)] (Nat.add x x2)) e2))))
  (Eq.r_trans Nat a (Nat.add a Nat.zero) b (Nat.add_zero_r a) (Eq.subst Nat x1 Nat.zero e3 [x : (Nat x)] (Eq Nat (Nat.add a x) b) e1))
